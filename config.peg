start
  = roles

// Common definitions
whitespace "whitespace"
  = [\n\t ]

notspecial
  = [a-zA-Z*0-9_\-.]

// Variables
varvalue "value"
  = notspecial+

varname "variable"
  = notspecial+

var
  = name: varname "=" value: varvalue
  { return [name.join(''), value.join('')] }

// handle optional delimeter and whitespace
nvar
 = v: var ","* whitespace*
 { return v }

vargroup
  = whitespace* "{" vars: nvar* "}"
  { var ret = {}; vars.forEach(function(i) { ret[i[0]] = i[1] } ); return ret;  }


// Roles
roleid "role name"
  = notspecial+

role
  = whitespace* name: roleid vars: vargroup
  { return [name.join(''), vars] }

// handle optional delimeter and whitespace
nrole
  = role: role ";"* whitespace*
  { return role;  }

roles
  = roles: nrole*
  { var ret = {}; roles.forEach(function(i) { ret[i[0]] = i[1] } ); return ret; }
