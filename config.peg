// This is a PEG.js grammar http://pegjs.org/
// Used for the config format of guv

start
  = roles

// Common definitions
whitespace "whitespace"
  = [\n\t ]

_ "spaces"
  = (" "*)?

notspecial
  = [a-zA-Z*0-9_\-.]

newline "newline"
  = [\n\r\u2028\u2029]

anychar "not newline"
  = [^\n\r\u2028\u2029]

// Comments
comment "comment"
  = newline* "#" anychar* newline* { return null; }

// Variables
varvalue "value"
  = notspecial+

varname "variable"
  = notspecial+

var
  = name: varname "=" value: varvalue
  { return [name.join(''), value.join('')] }

// handle optional delimeter and whitespace
nvar
 = v: var ","* whitespace*
 { return v }

vargroup
  = whitespace* "{" vars: nvar* "}"
  { var ret = {}; vars.forEach(function(i) { ret[i[0]] = i[1] } ); return ret;  }


// Roles
roleid "role name"
  = notspecial+

role
  = whitespace* name: roleid vars: vargroup
  { return [name.join(''), vars] }

// handle optional delimeter and whitespace
nrole
  = role: role ";"* whitespace*
  { return role;  }

roles
  = roles: (nrole / comment)*
  { var ret = {}; roles.forEach(function(i) {  if(i!=null) ret[i[0]] = i[1] } ); return ret; }
